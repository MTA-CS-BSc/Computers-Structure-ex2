/**
 * Let A be a binary numbers (8 bits) 
 * Zn = number of zeros in A.
 */

CHIP Ex2Q5 {
    IN A[8];
    OUT Zn[4];

    PARTS:
    Not(in=A[0], out=nota0);
    Not(in=A[1], out=nota1);
    Not(in=A[2], out=nota2);
    Not(in=A[3], out=nota3);
    Not(in=A[4], out=nota4);
    Not(in=A[5], out=nota5);
    Not(in=A[6], out=nota6);
    Not(in=A[7], out=nota7);

    // Main idea is to sum all different weights together by weight 
    HalfAdder(a=nota0, b=nota1, sum=s1w1, carry=c1w2);
    HalfAdder(a=nota2, b=nota3, sum=s2w1, carry=c2w2);
    HalfAdder(a=nota4, b=nota5, sum=s3w1, carry=c3w2);
    HalfAdder(a=nota6, b=nota7, sum=s4w1, carry=c4w2);

    HalfAdder(a=s1w1, b=s2w1, sum=s5w1, carry=c5w2);
    FullAdder(a=s5w1, b=s3w1, c=s4w1, sum=Zn[0], carry=c6w2);

    FullAdder(a=c1w2, b=c2w2, c=c3w2, sum=s1w2, carry=c1w4);
    FullAdder(a=c4w2, b=c5w2, c=c6w2, sum=s2w2, carry=c2w4);
    
    HalfAdder(a=s1w2, b=s2w2, sum=Zn[1], carry=c3w4);
    FullAdder(a=c1w4, b=c2w4, c=c3w4, sum=Zn[2], carry=Zn[3]);
}  